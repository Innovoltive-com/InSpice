* this uses the pmsm.lib. test how to use the .lib file
.title PMSM model with sinusoidal back EMF
.include "../../spice-library/electrical-machines/clark_park.lib"
.include "../../spice-library/electrical-machines/pmsm.lib"
.include "../../spice-library/semiconductors/operational-amplifier/generic_opamp.lib"

**input parameters
.param rs=3.4 
.param ls=12.1e-3 
.param poles=4
.param lambda_m=0.0827
.param J=5e-4
.param Bm=1e-9
.param vbus={11.25*sqrt(2)}


** Build the input voltage sources based on vqs_ref and vds_ref
* this will be the outside of the motor
Eas as as_n value={v(as_ref)}
Ebs bs bs_n value={v(bs_ref)}
Ecs cs cs_n value={v(cs_ref)}
Vas n as_n DC 0V
Vbs n bs_n DC 0V
Vcs n cs_n DC 0V
ran n 0 1e12
Xm as bs cs theta rpm tl pmsm
+rs={rs} ls={ls} 
+poles={poles} 
+lambda_m={lambda_m} 
+J={J} Bm={Bm}

Etl tl 0 value={0.4}


** Building the FOC contrller. 
Eias iasm 0 value={i(vas)}
Eibs ibsm 0 value={i(vbs)}
Eics icsm 0 value={i(vcs)}

* transform the current to alpha-beta frame
Xiab iasm ibsm icsm iam ibm clark
* m= measured phase currents
Xiqs iam ibm theta iqsm idsm park


* reference for pi controllers
*VIqref iq_ref 0 {2}
*VIdref id_ref 0 {0}
*Xpiq iq_ref iqsm vqs_ref pi
*Xpid id_ref idsm vds_ref pi


* generate the abc voltages
vqs_ref qs_ref 0 DC 0V PWL(0s 0V 1us {vbus})
vds_ref ds_ref 0 0

Xpark qs_ref ds_ref theta alpha_ref beta_ref park
* use reverse park transformation to get the reference voltages
Xiclark alpha_ref beta_ref as_ref bs_ref cs_ref iclark

.control
*options rshunt=1e12
*options noinit
*options klu
tran 0.1ms 2s 0s
plot qs_ref ds_ref
plot v(as) v(bs) v(cs)
plot i(vas) i(vbs) i(vcs)
plot v(as) i(vas)
plot v(rpm)
.endc


* a subckt for the PI controller using a generic opamp
.subckt pi meas ref out 
r1 meas inn 10k
c1 inn out 1u
vdd vdd 0 1
vss vss 0 0
*xop in+ in- vcc vee out genopa1 POLE=20 GAIN=20k VOFF=10m ROUT=10
xop  ref inn vdd vss out genopa1
.ends pi