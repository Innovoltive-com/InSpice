* generic OpAmp model
* gain, phase, offset, limits to power supply
.subckt genopa1 in+ in- vcc vee out params: POLE=20 GAIN=20k VOFF=10m ROUT=10
Voff in+ inoff dc {VOFF}
G10 0 int inoff in- 100u
R1 int 0 {GAIN/100u}
C1 int 0 {1/(6.28*(GAIN/100u)*POLE)}
Eout 2 0 int 0 1
Rout 2 out {ROUT}
Elow 3 0 vee 0 1
Ehigh 8 0 vcc 0 1
Dlow 3 int Dlimit
Dhigh int 8 Dlimit
.model Dlimit D N=0.01
.ends

* Generic differential-output OpAmp model
* gain, phase, offset, limits to power supply
.subckt genopadiff in+ in- vcc vee out+ out- vcm params: POLE=20 GAIN=20k VOFF=10m ROUT=10
Voff in+ inoff dc {VOFF}
G10 0 int inoff in- 100u
R1 int 0 {GAIN/100u}
C1 int 0 {1/(6.28*(GAIN/100u)*POLE)}
* Create differential outputs
Eoutp 2p 0 int 0 1
Eoutn 2n 0 0 int 1
Routp 2p out+ {ROUT}
Routn 2n out- {ROUT}
* Output clamping to rails
Elowp 3p 0 vee 0 1
Ehighp 8p 0 vcc 0 1
Dlowp 3p int Dlimit
Dhighp int 8p Dlimit

Elown 3n 0 vee 0 1
Ehighn 8n 0 vcc 0 1
Dlown 3n 0 Dlimit
Dhign 0 8n Dlimit

.model Dlimit D N=0.01
.ends
