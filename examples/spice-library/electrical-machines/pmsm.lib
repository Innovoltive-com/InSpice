* this code implements a permanent magnet synchronous machine (PMSM) model using abc model
* with a sinusoidal back EMF
* pmsm model. as, bs, cs are the phase voltages and theta is the electrical angle as output

.subckt pmsm as bs cs theta rpm tl
+rs=3.4 ls=12.1e-3 poles=4 
+lambda_m=0.0827 J=5e-4 Bm=1e-9
.include "clark_park.lib"
.param Lq={Ls}
.param Ld={Ls}

************ PMSM model. Inside the motor this is what hapens
vas as as0 DC 0V
vbs bs bs0 DC 0V
vcs cs cs0 DC 0V

ras as0 as1 {rs}
Las as1 as2 {ls}
Eas as2 n value={lambda_m*v(we)*cos(v(theta))}


rbs bs0 bs1 {rs}
Lbs bs1 bs2 {ls}
Ebs bs2 n value={lambda_m*v(we)*cos(v(theta) - 2*3.14/3)}

rcs cs0 cs1 {rs}
Lcs cs1 cs2 {ls}
Ecs cs2 n value={lambda_m*v(we)*cos(v(theta) - 4*3.14/3)}

* add a capacitor to ground to avoid floating nodes
can n 0 1e-9

Eias ias 0 value={i(vas)}
Eibs ibs 0 value={i(vbs)}
Eics ics 0 value={i(vcs)}
Xiab ias ibs ics ialpha ibeta clark
Xpark ialpha ibeta theta iqs ids park

* Flux linkage equations lambda_qs and lambda_ds 
Elqs lqs 0 value={Lq*v(iqs)}
Elds lds 0 value={Ld*v(ids) + lambda_m}

* Electrical Toque calculation, Te
Ete te 0 value={3/2 * poles/2 * (v(lds)*v(iqs) - v(lqs)*v(ids))}


* shaft speed (wm) calculation wm = Te-Tl /(J*s + Bm)
Etetl te_tl 0 value={v(te) - v(tl)}
Awm te_tl wm wmblk
.model wmblk s_xfer(num_coeff=[1] den_coeff=[{J} {Bm}] int_ic=[0])
Erpm rpm 0 value={v(wm)*60/2/3.14}

* electrical speed calculation
Ewe we 0 value={poles/2*v(wm)}

*TODO: need to make a roll-over integration blcok for theta
Atheta we theta thblk
.model thblk int(in_offset=0.0 gain=1.0
+ out_lower_limit=-1e12 out_upper_limit=1e12
+ limit_range=1e-9 out_ic=0.0)


.ends pmsm

